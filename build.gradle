plugins {
    id "se.bjurr.gitchangelog.git-changelog-gradle-plugin" version "1.71.4"
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'jacoco'

    sourceCompatibility = 15
    targetCompatibility = 15

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()

        maven { url "http://repo1.maven.org/maven2/" }
    }
}

group 'io.keepup.cms'
version '2.0-SNAPSHOT'

dependencies {
    compileClasspath "se.bjurr.gitchangelog:git-changelog-gradle-plugin:1.71.4"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    testLogging.showStandardStreams = true
    useJUnitPlatform()
}

subprojects {
    test.useTestNG()
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

task generateGitChangelogInlineTemplate(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
    fromRepo = file(".");
    file = new File("CHANGELOG.md");
    templateContent = """
# Changelog
Changelog
{{#tags}}
## {{name}}
 {{#issues}}
  {{#hasLink}}
### {{name}} [{{issue}}]({{link}}) {{title}}
  {{/hasLink}}
  {{^hasLink}}
### {{name}}
  {{/hasLink}}
  {{#commits}}
[{{hash}}](https://github.com/FedorSergeev/keepup/commit/{{hash}}) {{authorName}} *{{commitTime}}*
{{{message}}}
  {{/commits}}
 {{/issues}}
{{/tags}}
 """;
}

codeCoverageReport.dependsOn {
    subprojects*.test
}